#compdef appleopensource

# -----------------------------------------------------------------------------
# The BSD-3-Clause License
#
# Copyright (c) 2016, Koichi Shiraishi
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# * Redistributions of source code must retain the above copyright notice, this
#   list of conditions and the following disclaimer.
#
# * Redistributions in binary form must reproduce the above copyright notice,
#   this list of conditions and the following disclaimer in the documentation
#   and/or other materials provided with the distribution.
#
# * Neither the name of que nor the names of its
#   contributors may be used to endorse or promote products derived from
#   this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# POSSIBILITY OF SUCH DAMAGE.
# -----------------------------------------------------------------------------

function _appleopensource() {
  local context curcontext=$curcontext state line ret=1
  declare -A opt_args

  local -a commands

  commands=(
  'help:Show help'
  'cache:List cached project'
  'list:List all project available to opensource.apple.com'
  'versions:List all versions of the project available to opensource.apple.com'
  'release:List available macOS releases'
  )

  _arguments -C \
    "1: :{_describe 'appleopensource subcommand' commands}" \
    '*:: :->args' \
    && ret=0

  case $state in
    (args)
      case $words[1] in
        (cache)
          local -a cache_commands
          cache_commands=(
          'list:List cached project'
          'delete:Delete cache'
          )
          _arguments \
            "1: :{_describe 'cache subcommand' cache_commands}" \
            '*:: :->args' \
            && ret=0
            case $words[1] in
              (list)
                _arguments \
                  {-t,--tarballs}'[List tarballs type cache]' \
                  {-s,--source}'[List source type cache]' \
                  && ret=0
                ;;
            esac
          ;;
        (list)
          _arguments \
            {-t,--tarballs}'[List tarballs type cache]' \
            {-s,--source}'[List source type cache]' \
            && ret=0
          ;;

        (versions)
          _arguments \
            "1:project:($(appleopensource list))" \
            {-t,--tarballs}'[List the tarballs resource versions]' \
            {-s,--source}'[List the source resource versions]' \
            {-n,--no-cache}'[Disable the cache]' \
            && ret=0
          ;;

        (release)
          local -a release_commands
          release_commands=(
          'macos:macOS release'
          'xcode:Xcode release'
          'ios:iOS release'
          'server:macOS Server release'
          )
          _arguments \
            "1: :{_describe 'release command' release_commands}" \
            "2:Release version" \
            {-l,--list}'[List available releases]' \
            '*:: :->args' \
            && ret=0
          ;;

        help)
          _arguments "1: :{_describe 'command' commands}"
          ;;
      esac
      ;;
  esac

  return ret
}

_appleopensource "$@"

# vim:ft=zsh:et:sts=2:sw=2
